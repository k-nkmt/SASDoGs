/*##ExcludeFromDocumentation##*/

/*** HELP START ***//* 
Generate Sphinx documentation

Internal macro to generate Sphinx-format documentation files.
Sphinx is a powerful documentation generator widely used for technical documentation,
supporting multiple output formats including HTML, PDF, and ePub.

### Parameters

- **docsLocation**: Output directory path for generated documentation files
- **filesWithCodes**: Dataset containing information about package files with code
- **docDepth**: Documentation depth level for hierarchical organization
- **htmlTheme**: HTML theme for Sphinx documentation (default: `sphinx_book_theme`)
- **sphinxExt**: Sphinx extensions to include in `conf.py`
- **newConf**: Flag to create new `conf.py` even if one exists (0 = update existing, 1 = create new)

### Output
- `conf.py`: Sphinx configuration file
- `index.md`: Main documentation index
- `_static` folder: Static assets for Sphinx
- Additional markdown files based on docDepth setting

*//*** HELP END ***/

%macro _sphinxDoc(docsLocation =
                , filesWithCodes =
                , basePath =
                , docDepth =
                , htmlTheme = sphinx_book_theme
                , sphinxExt =
                , newConf = 0
                )/minoperator;

  %local  _CONFPY_ _INDEXMD_  exts _i ;

  %let _CONFPY_   = _%sysfunc(datetime(), hex6.)c;
  %let _INDEXMD_  = _%sysfunc(datetime(), hex6.)m;

  filename &_CONFPY_.   "&docsLocation./conf.py" lrecl = 4096;
  filename &_INDEXMD_.  "&docsLocation./index.md" lrecl = 4096;

  %if %superq(sphinxExt) ne %then
    %do _i = 1 %to %sysfunc(countw(%superq(sphinxExt))) ;
      %let exts = &exts. %bquote('%scan(%superq(sphinxExt), &_i., %str( ))') , ;
    %end;
    
  /* Generate conf.py */
  %if %sysfunc(fileexist(&docsLocation./conf.py)) and %superq(newConf)=0 %then
    %do;
      %put NOTE: Updating existing conf.py...;

      data _null_;
        infile &_CONFPY_. end=eof;
        file &_CONFPY_.;
        input;
        line = _infile_;
        
        length packageName packageVersion packageAuthor packageTitle $ 256 packageGenerated $ 32;
        packageName = symget("packageName");
        packageVersion = symget("packageVersion");
        packageAuthor = symget("packageAuthor");
        packageTitle = symget("packageTitle");
        packageGenerated = symget("packageGenerated");
        
        /* Update project info */
        if line =: "project =" then put 'project = "' packageName +(-1) '"';
        else if line =: "copyright =" then put 'copyright = "' "%sysfunc(year(%sysfunc(today()))), " packageAuthor +(-1) '"';
        else if line =: "author =" then put 'author = "' packageAuthor +(-1) '"';
        else if line =: "release =" then put 'release = "' packageVersion +(-1) '"';
        else if line =: "version =" then put 'version = "' packageVersion +(-1) '"';
        else put _infile_;
      run;
      
    %end;
  %else
    %do;
      %put NOTE: Creating new conf.py...;
      data _null_;
        file &_CONFPY_.;
        
        length packageName packageVersion packageAuthor packageTitle $ 256
              packageGenerated $ 32;
        packageName = symget("packageName");
        packageVersion = symget("packageVersion");
        packageAuthor = symget("packageAuthor");
        packageTitle = symget("packageTitle");
        packageGenerated = symget("packageGenerated");
        confGenerated = put(datetime(), E8601DT19.);
        
        put '# Configuration file for the Sphinx documentation builder.';
        put '#';
        put '# This file was auto-generated by SASDoGs' ;
        put '# Generated: ' confGenerated;
        put '#';
        put '# For the full list of built-in configuration values, see the documentation:';
        put '# https://www.sphinx-doc.org/en/master/usage/configuration.html';
        put ' ';
        put '# -- Project information -----------------------------------------------------';
        put '# https://www.sphinx-doc.org/en/master/usage/configuration.html#project-information';
        put ' ';
        put 'project = "' packageName +(-1) '"';
        put 'copyright = "' "%sysfunc(year(%sysfunc(today()))), " packageAuthor +(-1) '"';
        put 'author = "' packageAuthor +(-1) '"';
        put 'release = "' packageVersion +(-1) '"';
        put 'version = "' packageVersion +(-1) '"';
        put ' ';
        put '# -- General configuration ---------------------------------------------------';
        put '# https://www.sphinx-doc.org/en/master/usage/configuration.html#general-configuration';
        put ' ';
        put "extensions = [";
        put "    'sphinx.ext.githubpages',";
        put "    'sphinx.ext.mathjax',";
        put "    'myst_parser',";
        put "    &exts." ;
        put "]";
        put ' ';
        put "templates_path = ['_templates']";
        put "exclude_patterns = ['_build', 'Thumbs.db', '.DS_Store']";
        put ' ';
        put '# -- Options for HTML output -------------------------------------------------';
        put '# https://www.sphinx-doc.org/en/master/usage/configuration.html#options-for-html-output';
        put ' ';
        put "html_theme = '" "&htmlTheme." "'";
        put "html_static_path = ['_static']";
        put "highlight_language= 'sas'";
        put "pygments_light_style = 'sas'";
        put ' ';
        put '# -- Options for MyST parser (Markdown support) ------------------------------';
        put "myst_enable_extensions = [";
        put "    'dollarmath',";
        put "    'amsmath',";
        put "    'colon_fence',";
        put "    'deflist',";
        put "]";

        put "myst_heading_anchors = 3";
        put ' ';
        put '# -- Options for todo extension ----------------------------------------------';
        put "todo_include_todos = True";
        
        stop;
      run;
    %end;

  /* Sort content by type and name */
  data &filesWithCodes.sphinx;
    /* break if no data */
    if NOBS = 0 then stop;

    EOFDS = 0;
    do until(EOFDS);
      /* content is created during package creation */
      set &filesWithCodes. end = EOFDS nobs = NOBS curobs = CUROBS;
      if upcase(type) in: ('TEST') then continue; /* exclude tests */
      
      /*
        To exclude file from being added to the documentation 
        insert the "excluding" text(see below) as a comment 
        in the FIRST or SECOND line of the file! 
        Do not add spaces.
        
        For each file the first line is read in and checked.
      */
      _FILEVARPATH_ = path ;
      infile _dummy_ FILEVAR= _FILEVARPATH_ ;
      input;
      if strip(_infile_) IN (
          '/*##DoNotUse4Documentation##*/'
          '/*##ExcludeFromDocumentation##*/'
          '/*##ExcludeFromSphinxDoc##*/'
        )
      then continue; /* exclude file from documentation after FIRST line */
      input;
      if strip(_infile_) IN (
          '/*##DoNotUse4Documentation##*/'
          '/*##ExcludeFromDocumentation##*/'
          '/*##ExcludeFromSphinxDoc##*/'
        )
      then continue; /* exclude file from documentation after SECOND line */
                    /* this is because %splitCodeForPackage() macro adds one extra line */ 
      
      output;
    end;

    stop;
  run; 


  /* Generate separate MD files for each type or each file */
  %generateMD(fileList=&filesWithCodes.sphinx, docsLocation=&docsLocation., depth=&docDepth.) ;

  %_indexMD(filesWithCodes=&filesWithCodes.sphinx, idxref = &_INDEXMD_., engine = SPHINX, depth=&docDepth.) ;

  libname _static_ "&docsLocation./_static";
  libname _static_ clear;

  %put NOTE: Sphinx files generated in: &docsLocation.;
  %put NOTE- conf.py: &docsLocation./conf.py;
  %put NOTE- index.md: &docsLocation./index.md;
  %put NOTE- ;

  filename &_CONFPY_. clear;
  filename &_INDEXMD_. clear;

%mend _sphinxDoc;