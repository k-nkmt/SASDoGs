/*##ExcludeFromDocumentation##*/

/*** HELP START ***//* 
Generate MyST Markdown documentation

Internal macro to generate MyST (Markedly Structured Text) format documentation.
MyST is used by Jupyter Book for creating rich, interactive documentation
with enhanced markdown features.

### Parameters

- **docsLocation**: Output directory path for generated documentation files
- **filesWithCodes**: Dataset containing information about package files with code
- **docDepth**: Documentation depth level for content organization
- **mystTheme**: MyST theme for documentation rendering (default: `book-theme`)
- **newConf**: Flag to create new `myst.yml` even if one exists (0 = update existing, 1 = create new)

### Usage

This is an internal macro called by `%packageDoc` when `engine=MYST`.
Should not be called directly by users.

*//*** HELP END ***/

%macro _mystDoc(docsLocation =
              , filesWithCodes =
              , docDepth =
              , mystTheme = book-theme
              , newConf = 0
              )/minoperator;

  %if not(%superq(mystTheme) in (%bquote(book-theme article-theme))) %then 
  %do;
    %put ERROR: Invalid mystTheme value "&mystTheme.". Valid options are: book-theme, article-theme.;
    %abort;
  %end;

  %local _MYSTYML_ _INDEXMD_ _STYLECSS_ _BKYML_;
  %let _MYSTYML_   = _%sysfunc(datetime(), hex6.)y;
  %let _INDEXMD_  = _%sysfunc(datetime(), hex6.)m;
  %let _STYLECSS_ = _%sysfunc(datetime(), hex6.)s;
  %let _BKYML_ = _%sysfunc(datetime(), hex6.)b;

  filename &_MYSTYML_.  "&docsLocation./myst.yml" lrecl = 4096;
  filename &_INDEXMD_.  "&docsLocation./index.md" lrecl = 4096;
  filename &_STYLECSS_. "&docsLocation./style.css" lrecl = 4096;

  /* Generate myst.yml */
  %if %sysfunc(fexist(&_MYSTYML_.)) and %superq(newConf)=0 %then
    %do;
      %put NOTE: Updating existing myst.yml (only TOC section)...;
      
      /* Create backup with TOC section info */
      filename &_BKYML_. temp lrecl = 4096;
      
      /* Parse existing file: save pre-TOC, TOC (commented), and post-TOC sections */
      data _null_;
        infile &_MYSTYML_. end=eof;
        file &_BKYML_.;
        retain inTOC 0;
        input;
        if _infile_ = "  toc:" then inTOC = 1;
        if _infile_ = "site:" then inTOC = 0;
        if inTOC = 1 then 
          put "# "  _infile_ ;
        else  
          put _infile_;
      run ;

      data _null_;
        infile &_BKYML_. end=eof;
        file &_MYSTYML_.;
        input;
        if _infile_ = "site:" then 
          do ;
            put '  toc:';
            put '    - file: index.md';
            stop ;
          end ;
          
        put _infile_;
      run ;
    %end ;
  %else 
    %do;
      %put NOTE: Creating new myst.yml...;

      data _null_;
        file &_MYSTYML_.;
        length packageName packageVersion packageAuthor packageTitle packageLicense  $ 256;
        packageName = symget("packageName");
        packageVersion = symget("packageVersion");
        packageAuthor = symget("packageAuthor");
        packageTitle = symget("packageTitle");
        packageLicense = symget("packageLicense");
        confGenerated = put(datetime(), E8601DT19.);

        /* Basic configuration */
        put '# Configuration file for the MyST documentation builder.';
        put '#';
        put '# This file was auto-generated by SASDoGs' ;
        put '# Generated: ' confGenerated;
        put '#';
        put '# For the full list of lists the available frontmatter fields, see the documentation:';
        put '# https://mystmd.org/guide/frontmatter';
        put 'version: 1';
        put ' ';
        put 'project:';
        put '  title: ' packageTitle;
        put '  license: ' packageLicense;
        put '  keywords: []';
        put '  # github: ';
        put ' ';
        
        /* TOC section start */
        put '# The Table of Contents defines the structure of your MyST project. ' ;
        put '# https://mystmd.org/guide/table-of-contents' ;
        put '  toc:';
        put '    - file: index.md';
        
        stop;
      run;
    %end;

  /* Sort content by type and name */
  data &filesWithCodes.myst;
    /* break if no data */
    if NOBS = 0 then stop;

    EOFDS = 0;
    do until(EOFDS);
      /* content is created during package creation */
      set &filesWithCodes. end = EOFDS nobs = NOBS curobs = CUROBS;
      if upcase(type) in: ('TEST') then continue; /* exclude tests */
      
      /*
        To exclude file from being added to the documentation 
        insert the "excluding" text(see below) as a comment 
        in the FIRST or SECOND line of the file! 
        Do not add spaces.
        
        For each file the first line is read in and checked.
      */
      _FILEVARPATH_ = path ;
      infile _dummy_ FILEVAR= _FILEVARPATH_ ;
      input;
      if strip(_infile_) IN (
          '/*##DoNotUse4Documentation##*/'
          '/*##ExcludeFromDocumentation##*/'
          '/*##ExcludeFromMySTDoc##*/'
        )
      then continue; /* exclude file from documentation after FIRST line */
      input;
      if strip(_infile_) IN (
          '/*##DoNotUse4Documentation##*/'
          '/*##ExcludeFromDocumentation##*/'
          '/*##ExcludeFromMySTDoc##*/'
        )
      then continue; /* exclude file from documentation after SECOND line */
                    /* this is because %splitCodeForPackage() macro adds one extra line */ 
      
      output;
    end;

    stop;
  run; 

  /* Generate TOC based on depth */
  %if %superq(docDepth)=1 %then
    %do;
      /* Depth 1: No children, just file list grouped by type */
      data _null_;
        if 0 = NOBS then stop;
        
        file &_MYSTYML_. mod;
        set &filesWithCodes.myst end=EOF nobs=NOBS;
        by group;
        
        length fname $ 256;
        fname = cats(type, ".md");
        
        if first.group then
          put '    - file: ' fname;
      run;
    %end;
  %else %if %superq(docDepth)=2 %then
    %do;
      /* Depth 2: Hierarchical with type as title and files as children */
      data _null_;
        if 0 = NOBS then stop;
        
        file &_MYSTYML_. mod;
        set &filesWithCodes.myst end=EOF nobs=NOBS;
        by group;
        
        length fname $ 256;
        fname = cats(type, "_", fileshort, ".md");
        
        if first.group then
          do;
            put '    - title: ' type;
            put '      children:';
          end;
        
        put '        - file: ' fname;
      run;
    %end;

  /* Continue with site configuration */
  %if %sysfunc(fileref(&_BKYML_.)) and %superq(newConf)=0 %then
    %do;
      data _null_;
        file &_MYSTYML_. mod;
        infile &_BKYML_. end=eof;
        input;
        retain inSite 0;
        if _infile_ = "site:" then 
          inSite = 1;
        if inSite = 1 then
          put _infile_;
      run ;

      filename &_BKYML_. clear;
    %end;
  %else 
    %do;
      data _null_;
        file &_MYSTYML_. mod;
        
        put ' ';
        put '# Site options allow you to configure a themeâ€™s behavior.';
        put '# https://mystmd.org/guide/website-templates';
        put 'site:';
        put "  template: &mystTheme.";
        put "  title: &packageTitle.";
        put '  options:';
        put '    # logo: pkg_image.png';
        put "    logo_text: &packageName.";
        put '    style: style.css';
        put '  # actions:';
        put '  #   - title: SASPAC';
        put '  #     url: https://github.com/SASPAC';
        put '  #   - title: PharmaForest';
        put '  #     url: https://github.com/PharmaForest';

        stop;
      run;
    %end ;

  /* Generate separate MD files for each type or each file */
  %generateMD(fileList=&filesWithCodes.myst, docsLocation=&docsLocation., depth=&docDepth.) ;
  %_indexMD(filesWithCodes=&filesWithCodes.myst, idxref = &_INDEXMD_., engine = MYST, depth=&docDepth.) ;

  /* Generate style.css */
  %if %sysfunc(fexist(&_STYLECSS_.)) = 0 or %superq(newConf)=1 %then
    %do;  
      %put NOTE: Creating style.css...;
      data _null_;
        file &_STYLECSS_.;
        put '/* Modify SAS Html styles conflicting with Jupyter Book styles */';
        put 'a:link, a:visited {color:inherit !important;}';
        put '.jp-OutputArea table.table{color: rgba(0, 0, 0, .87);}';
        stop;
      run;
    %end ;

  %put NOTE: MyST files generated in: &docsLocation.;
  %put NOTE- myst.yml: &docsLocation./myst.yml;
  %put NOTE- index.md: &docsLocation./index.md;
  %put NOTE- style.css: &docsLocation./style.css;
  %put NOTE- ;

  filename &_MYSTYML_. clear;
  filename &_INDEXMD_. clear;
  filename &_STYLECSS_. clear;

%mend _mystDoc;