/*##ExcludeFromDocumentation##*/

/*** HELP START ***//* 
Generate index.md documentation file

Internal macro to generate the main `index.md` file for package documentation.
Creates a formatted markdown file with package metadata, version information,
and organized content listings.

### Parameters

- **filesWithCodes**: Dataset containing information about files with code
- **idxref**: File reference for the output `index.md` file
- **depth**: Documentation depth level for hierarchical organization
- **engine**: Documentation engine type (`SPHINX`, `MYST`)

*//*** HELP END ***/

/* Generate index.md */

%macro _indexmd(filesWithCodes = 
              , idxref        = 
              , depth         =
              , engine        =
              );

  %local _PackageFileref_ device nolicense;
  %let _PackageFileref_ = _%sysfunc(datetime(), hex6.)_ ;

  data _null_ ;
    set &filesWithCodes. ;
    length device $4 ;
    nolicense = 0 ;

    if prxmatch("/\.zip$/i", strip(base)) > 0 then
      device = "zip" ;
    else
      do;
        device = "disk" ;
        nolicense = (fileexist(cats(base,'/license.sas')) = 0 ) ;
      end;

    call symputx("device", device, "L") ;
    call symputx("nolicense", nolicense, "L") ;
    rc = filename("&_PackageFileref_.", base, device) ;
    stop ;
  run ;

  proc sort data=&filesWithCodes.;
    by group fileshort;
  run;

  data _null_;
    
    file &idxref.;
    
    length packageName packageVersion packageTitle packageAuthor packageMaintainer packageLicense $ 256
          packageGenerated $ 32
          packageHashF packageHashC $ 128;
    packageName = symget("packageName");
    packageVersion = symget("packageVersion");
    packageTitle = symget("packageTitle");
    packageAuthor = symget("packageAuthor");
    packageMaintainer = symget("packageMaintainer");
    packageLicense = symget("packageLicense");
    packageGenerated = symget("packageGenerated");
    packageHashF = symget("packageHashF");
    packageHashC = symget("packageHashC");
    
    /* Header */
    put '# Documentation for the `' packageName +(-1) '` package';
    put ' ';
    
    /* Version information */
    put ' ';
    put packageTitle;
    put ' ';
    put ' ';
    put '## Version Information';
    put ' ';
    put '* **Package:** ' packageName;
    put '* **Version:** ' packageVersion;
    put '* **Generated:** ' packageGenerated;
    put '* **Author(s):** ' packageAuthor;
    put '* **Maintainer(s):** ' packageMaintainer;
    put '* **License:** ' packageLicense;
    if not missing(packageHashF) then
      put '* **File SHA256:** `' packageHashF +(-1) '`';
    if not missing(packageHashC) then
      put '* **Content SHA256:** `' packageHashC +(-1) '`';
    put ' ';
    
    %if %superq(packageRequired) ne %then
    %do;
      put '### Required SAS Components';
      put ' ';
      length req $ 256;                        
      do req = &packageRequired. ;      
        put '* ' req;                    
      end;
      put ' ';
    %end;

    %if %superq(packageReqPackages) ne %then
    %do;
      put '### Required SAS Packages';
      put ' ';
      length req2 $ 256;                     
      do req2 = &packageReqPackages.;
        put '* ' req2;                 
      end;
      put ' ';
    %end;

    %if %superq(additionalContent) NE %then
    %do;
      put "  " / "---" / " ";
      put 'Package contains additional content, run:  `%loadPackageAddCnt(' "&packageName." ')`  to load it'
        / "or look for the `%sysfunc(lowcase(&packageName.))_AdditionalContent` directory in the `packages` fileref"
        / "localization (only if additional content was deployed during the installation process).";
    %end;
    %if %superq(pkgSPFVer) NE %then
    %do;
    put " " / "---------------------------------------------------------------------" / " " 
            / "*SAS package generated by SAS Package Framework, version `&pkgSPFVer.`,*"
            / "*under &pkgOSVer. operating system,*" 
            / "*using SAS release: `&pkgSASVer.`*" 
      / " " / "---------------------------------------------------------------------" / " ";
    %end;

    /* Description section */
    put '## Description';
    put ' ';
    
    /* Read description from package */
    do until (EOF);
      infile &_PackageFileref_.(description.sas) end = EOF;
      input;
      if upcase(strip(_infile_)) =: "DESCRIPTION END:"   then printer = 0;
      if printer then put _infile_;
      if upcase(strip(_infile_)) =: "DESCRIPTION START:" then printer = 1;
    end;
    
    put ' ';
    put '## Contents';
    put "The `&packageName.` package consists of the following content:" / " ";
    put ' ';
    stop;
  run;


  %if %superq(engine)=SPHINX %then
    %do;
      data _null_;
        /* break if no data */
        if 0 = NOBS then stop; 

        file &idxref. mod;
        set &filesWithCodes end = EOF nobs = NOBS; 
        by group ;
        
        %if %superq(depth)=1 %then
          %do;
            fname = cats(type, ".md");
            if _n_ = 1 then 
              do;
                put "```{toctree}";
                put ":hidden:";
                put ":glob:";
                put ":caption: Content";
              end;
            if first.group then put fname;
            if EOF then put "```"; 
          %end;
        %else %if %superq(depth)=2 %then
          %do;
            fname = cats(type, "_", fileshort, ".md");
            if first.group then 
              do;
                put "```{toctree}";
                put ":hidden:";
                put ":glob:";
                put ":caption:" +1 type;
              end;
            put fname;
            if last.group then put "```"; 
          %end;
      run ;
    %end;

  /* Append links to index.md */
  data _null_;
    /* break if no data */
    if 0 = NOBS then stop; 

    file &idxref. mod;
    set &filesWithCodes. nobs = NOBS;
    by group;
    
    /* List of types (internal links) */
    if first.group then do;
      put '* [' type '](#' type ')';
    end;
  run;

  data _null_;
    /* break if no data */
    if 0 = NOBS then stop; 

    file &idxref. mod;
    set &filesWithCodes. nobs = NOBS;
    by group;

    /* Sections for each type */
    if first.group then do;
      put ' ';
      put '### ' type;
      put ' ';
    end;
    
    /* Links to items in separate MD files */
    %if %superq(depth)=1 %then
      %do;
        link = cats('* [', fileshort2, type2, '](', type, '.md#', fileshort, ')');
      %end;
    %else %if %superq(depth)=2 %then
      %do;
        link = cats('* [', fileshort2, type2, '](', type, "_", fileshort, '.md)');
      %end;
    put link;
  run;

  %if %superq(nolicense) = 1 %then
    %do ;
      %put WARNING: License file not found in the package directory.;
      %goto nolicense;
    %end;

  data _null_;
    file &idxref. mod;
    put ' ';
      
    /* License section */
    put '## License';
    put ' ';
    put '```text';
    do until (EOF_L);                                            
      infile &_PackageFileref_.(license.sas) end = EOF_L;        
      input;                                                   
      put _infile_;                                       
    end;
    put '```';
    put ' ';
    
    stop;
  run;

  %nolicense:

  filename &_PackageFileref_. clear;

%mend _indexmd;