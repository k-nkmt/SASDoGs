/*##ExcludeFromDocumentation##*/

/*** HELP START ***//* 
Generate SPF (SAS Packages Framework) documentation

Internal macro to generate standard markdown documentation compatible with
the SAS Packages Framework (SPF). Creates a single consolidated markdown file
containing all package documentation.

### Parameters

- **docsLocation**: Output directory path for generated documentation file
- **filesWithCodes**: Dataset containing information about package files with code

### Output

Creates a single markdown file named `<packagename>.md` in the docsLocation containing:
- Package metadata and version information
- Organized listings of all package components
- Extracted help documentation from source files

*//*** HELP END ***/

%macro _spfDoc(docsLocation   =
             , filesWithCodes =
             ) ;
              
%put NOTE-;
%put NOTE: Preparing markdown documentation file.;
%put NOTE- ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^;
%put NOTE-;

%local _mdFileref_ _PackageFileref_ device nolicense;
%let _PackageFileref_ = _%sysfunc(datetime(), hex6.)_ ;
%let _mdFileref_      = _%sysfunc(datetime(), hex6.)m ;

  data _null_ ;
    set &filesWithCodes. ;
    length device $4 ;
    nolicense = 0 ;

    if prxmatch("/\.zip$/i", strip(base)) > 0 then
      device = "zip" ;
    else
      device = "disk" ;
      nolicense = (fileexist(cats(base,'/license.sas')) = 0 ) ;

    call symputx("device", device, "L") ;
    call symputx("nolicense", nolicense, "L") ;
    rc = filename("&_PackageFileref_.", base, device) ;
    stop ;
  run ;

filename &_mdFileref_. "&docsLocation./%sysfunc(lowcase(&packageName.)).md";

data &filesWithCodes.markdown;
  /* break if no data */
  if NOBS = 0 then stop;

  file &_mdFileref_. encoding = &packageEncoding.;
  put "# Documentation for the `&packageName.` package.";

  length packageLicense packageGenerated $ 64
         packageTitle packageAuthor packageMaintainer $ 4096 
         packageHashF packageHashC $ 128 
         ;
  packageLicense=symget("packageLicense");
  packageTitle=symget("packageTitle");
  packageGenerated=symget("packageGenerated");
  packageAuthor=symget("packageAuthor");
  packageMaintainer=symget("packageMaintainer");
  packageHashF=symget("packageHashF");
  packageHashC=symget("packageHashC");
  drop package:;

  put "  " / 64*"-" / " "
    / ' *' packageTitle +(-1)'* '
    / "  " / 64*"-" / " "
    / "### Version information:"
    / "  "  
    / "- Package: &packageName."
    / "- Version: &packageVersion." 
    / "- Generated: " packageGenerated
    / "- Author(s): " packageAuthor
    / "- Maintainer(s): " packageMaintainer
    / "- License: " packageLicense
    / "- File SHA256: `" packageHashF +(-1) "` for this version"
    / "- Content SHA256: `" packageHashC  +(-1) "` for this version"
    / "  " / "---" / " ";

  put "# The `&packageName.` package, version: `&packageVersion.`;"
    / "  " / "---" / " ";

  do until (EOF);                                                        
    infile &_PackageFileref_.(description.sas) end = EOF;                
    input;                                                               
    if upcase(strip(_infile_)) =: "DESCRIPTION END:"   then printer = 0; 
    if printer then put _infile_;                                   
    if upcase(strip(_infile_)) =: "DESCRIPTION START:" then printer = 1; 
  end;                                                                   

  put "  " / "---" / " ";

  %if %superq(packageRequired) ne %then
  %do;
    put "  " / "---" / " ";
    length req $ 256;                        
    put "Required SAS Components: ";
    do req = &packageRequired. ;      
      put @3 "-" @5 req;                    
    end ;                                   
  %end;

  %if %superq(packageReqPackages) ne %then
  %do;
    put "  " / "---" / " ";
    length req2 $ 256;                     
    put "Required SAS Packages: ";     
    do req2 = &packageReqPackages.;
      put @3 "-" @5 req2;                 
    end ;                                
  %end;

  put "  " / "---" / " ";

  %if %superq(additionalContent) NE %then
  %do;
    put "  " / "---" / " ";
    put 'Package contains additional content, run:  `%loadPackageAddCnt(' "&packageName." ')`  to load it'
      / "or look for the `%sysfunc(lowcase(&packageName.))_AdditionalContent` directory in the `packages` fileref"
      / "localization (only if additional content was deployed during the installation process).";
  %end;

  %if %superq(pkgSPFVer) NE %then
  %do;
    put " " / "---------------------------------------------------------------------" / " " 
            / "*SAS package generated by SAS Package Framework, version `&pkgSPFVer.`,*"
            / "*under &pkgOSVer. operating system,*" 
            / "*using SAS release: `&pkgSASVer.`*" 
      / " " / "---------------------------------------------------------------------" / " ";
  %end;

  put "# The `&packageName.` package content";
  put "The `&packageName.` package consists of the following content:" / " ";
  EOFDS = 0;
  do until(EOFDS);
    /* content is created during package creation */
    set &filesWithCodes. end = EOFDS nobs = NOBS curobs = CUROBS;
    if upcase(type) in: ('TEST') then continue; /* exclude tests */
    
    /*
      To exclude file from being added to the documentation 
      insert the "excluding" text(see below) as a comment 
      in the FIRST or SECOND line of the file! 
      Do not add spaces.
      
      For each file the first line is read in and checked.
    */

     _FILEVARPATH_ = path ;
    infile _dummy_ FILEVAR= _FILEVARPATH_ ;
    input;
    if strip(_infile_) IN (
        '/*##DoNotUse4Documentation##*/'
        '/*##ExcludeFromDocumentation##*/'
        '/*##ExcludeFromMarkdownDoc##*/'
      )
    then continue; /* exclude file from documentation after FIRST line */
    input;
    if strip(_infile_) IN (
        '/*##DoNotUse4Documentation##*/'
        '/*##ExcludeFromDocumentation##*/'
        '/*##ExcludeFromMarkdownDoc##*/'
      )
    then continue; /* exclude file from documentation after SECOND line */
                   /* this is because %splitCodeForPackage() macro adds one extra line */ 
    
    contentObs + 1;
    put @1 contentObs +(-1) '. [' fileshort2 type2'](#' link ')';
    output;
  end;
  put "  " / " ";

  %if %superq(nolicense)=0 %then
    %do;
      contentObs+1;
      put @1 contentObs +(-1) '. [License note](#license)';
      put "  " / "---" / " ";
    %end ;

  putlog "Doc. note with general information ready.";
  stop;
run;

%generateMD(fileList=&filesWithCodes.markdown, docsLocation=&docsLocation.);

/* license info */
%if %superq(nolicense)=1 %then
  %do;
    %put NOTE: License file not found in the package source, skipping license section.;
    %goto nolicense;
  %end;

data _null_;
  file &_mdFileref_. encoding = &packageEncoding. MOD;
  putlog "Doc. note with license ready.";
  put "  " / "---" / " "
    / '# License <a name="license"></a> ######' / " "
    ;
  do until (EOF_L);                                            
    infile &_PackageFileref_.(license.sas) end = EOF_L;        
    input;                                                   
    put _infile_;                                       
  end;                  
  put "  " / "---" / " ";
  stop;
run;

%nolicense:

%put NOTE: Markdown file generated.;
filename &_mdFileref_. list;
%put NOTE- ;

filename &_mdFileref_. clear;
filename &_PackageFileref_. clear;

%mend _spfDoc;
